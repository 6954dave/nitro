import sys, os, glob, shutil

sys.path.append('../../build/utils')
import scons_utils
sys.path.pop()

################################################################################
# PROJECT SPECIFIC VARS/CODE
################################################################################
#==============================================================================#

LIB_NAME = 'XMLTRE'

# Handle command-line options
opts = scons_utils.addDefaultOptions(Options())
opts.Add(PathOption('xmlhome', 'the path of the libxml2 installation directory', None))
env = Environment(options = opts, ENV = os.environ)
Help(opts.GenerateHelpText(env))

# Get the target name, and setup all the system-dependent flags and libs
local_lib = scons_utils.doConfigure(env)
print "Building %s for %s" % (LIB_NAME, env['PLATFORM'])

include_path = ['../../c/nitf/include']
lib_path = ['../../c/nitf/%s' % local_lib]

depends = ['nitf-c']
if sys.platform.startswith('win32'):
    depends.append('Ws2_32') # for ntohs on Windows

if env.subst('$xmlhome'):
    xml_home = env.subst('$xmlhome')
    include_path.append(os.path.normpath(os.path.join(xml_home, 'include')))
    xml_libdir = os.path.normpath(os.path.join(xml_home, 'lib'))
    lib_path.append(xml_libdir)
    for dep in ['xml2.lib', 'libxml2.lib']:
        if os.path.exists(os.path.join(xml_libdir, dep)):
            depends.append(dep)
elif sys.platform.startswith('win32'):
    #unzip the binaries
    scons_utils.extract('build/libxml2-2.6.32.zip', './')
    shutil.copy('build/SConstruct_xml', 'libxml2-2.6.32/SConstruct')
    SConscript(["libxml2-2.6.32/SConstruct",],)
    include_path.append(os.path.join('libxml2-2.6.32', 'include'))
    lib_path.append(os.path.join('libxml2-2.6.32', local_lib))
    depends.append('libxml2.lib')

sourceFiles = scons_utils.getSourceFiles('./source', ext='.c', platform=env['PLATFORM'])
build_libs = [{'source':sourceFiles, 'lib':'%s' % LIB_NAME, 'dynamic':True,
        'depends':depends}]


env.Append(CPPPATH = include_path)
env.Append(CCFLAGS = ['-DNITF_MODULE_EXPORTS', '-ULIBXML_ICONV_ENABLED'])

libs = scons_utils.makeLibs(env, build_libs, local_lib, lib_path)

#==============================================================================#
